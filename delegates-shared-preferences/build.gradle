apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'org.jetbrains.dokka-android'
apply plugin: 'maven'

android {

	compileSdkVersion 25
	buildToolsVersion "25.0.3"

	defaultConfig {
		minSdkVersion 14
		targetSdkVersion 25
		versionCode androidVersionCode.toInteger()
		versionName androidVersionName
		testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
	}

	buildTypes {
		release {
			minifyEnabled false
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
		}
	}

	sourceSets {
		main.java.srcDirs += 'src/main/kotlin'
		test.java.srcDirs += 'src/test/kotlin'
		androidTest.java.srcDirs += 'src/androidTest/kotlin'
	}
}

dependencies {
	compile fileTree(dir: 'libs', include: ['*.jar'])

	compile "org.jetbrains.kotlin:kotlin-stdlib-jre7:${kotlinVersion}"

	testCompile 'junit:junit:4.12'

	androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
		exclude group: 'com.android.support', module: 'support-annotations'
	})
}

dokka {
	moduleName = 'SharedPreferences delegates for Android'
	outputFormat = 'javadoc'
	outputDirectory = "$buildDir/javadoc"
	sourceDirs = android.sourceSets.main.java.srcDirs
	linkMapping {
		dir = "src/main/kotlin"
		url = "https://github.com/sczerwinski/android-delegates-shared-preferences/blob/master/src/main/kotlin"
		suffix = "#L"
	}
}

task dokkaJar(type: Jar, dependsOn: project.dokka) {
	classifier = 'javadoc'
	from dokka
}

task sourcesJar(type: Jar) {
	classifier = 'sources'
	from android.sourceSets.main.java.srcDirs
}

artifacts {
	archives dokkaJar
	archives sourcesJar
}

uploadArchives {
	repositories.mavenDeployer {
		pom.groupId = "${groupId}"
		pom.artifactId = "${artifactId}"
		pom.version = "${androidVersionName}"

		repository(url: "file://${buildDir}/release/${androidVersionName}")
	}
}
